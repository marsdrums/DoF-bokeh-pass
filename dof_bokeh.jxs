<jittershader name="default">

	<description>Default Slab </description>
	<param name="renderTex" type="int" default="0" />
	<param name="depthTex" type="int" default="1" />
	<param name="sampledDepthTex" type="int" default="2" />
	<param name="blueNoiseTex" type="int" default="3" />
	<texture file="blue_noise_vec2.png" type="float16" unit="3" rectangle="1" filter="none none" wrap="repeat repeat"/>	

	<param name="modelViewProjectionMatrix" type="mat4" state="MODELVIEW_PROJECTION_MATRIX" />
	<param name="textureMatrix0" type="mat4" state="TEXTURE0_MATRIX" />
	<param name="position" type="vec3" state="POSITION" />
	<param name="texcoord" type="vec2" state="TEXCOORD" />
	<param name="nearClip" type="float" state="NEAR_CLIP" />
	<param name="farClip" type="float" state="FAR_CLIP" />
	<param name="texDim" type="vec2" state="TEXDIM0" />

	<param name="lens_focal_distance" type="float" default="4.0" />
	<param name="lens_focal_length" type="float" default="0.8" />
	<param name="lens_fstop" type="float" default="2.0" />
	<param name="num_samples" type="int" default="4" />			//samples on the first ring
	<param name="num_rings" type="int" default="4" />			//ring count
	<param name="lens_coc" type="float" default="0.03" />		//circle of confusion size in mm (35mm film = 0.03mm)
	<param name="lens_max_blur" type="float" default="2." />			//clamp value of max blur (0.0 = no blur,1.0 default)

	<param name="bokeh_threshold" type="float" default="3.0" />	//highlight bokeh_threshold;
	<param name="bokeh_gain" type="float" default="10.0" />		//highlight bokeh_gain;
	<param name="bokeh_pentagon_enable" type="int" default="0" />		//enable pentagon shaped bokeh
	<param name="bokeh_bias" type="float" default="0.5" />			//bokeh edge bokeh_bias
	<param name="bokeh_fringe" type="float" default="0.7" />			//bokeh chromatic aberration/fringing
	<param name="noise_amount" type="float" default="0.0001" />		//dither amount
	<param name="bokeh_feather" type="float" default="1.4" />			//pentagon shape bokeh_feather

	<param name="show_in_focus" type="int" default="0" />
	<param name="autofocus_enable" type="int" default="0" />
	<param name="autofocus_coordinates" type="vec2" default="0.5 0.5" />

	<language name="glsl" version="1.5">

		<bind param="renderTex" program="fp" />
		<bind param="depthTex" program="fp" />
		<bind param="sampledDepthTex" program="fp" />
		<bind param="blueNoiseTex" program="fp" />

		<bind param="modelViewProjectionMatrix" program="vp" />
		<bind param="textureMatrix0" program="vp" />
		<bind param="position" program="vp" />
		<bind param="texcoord" program="vp" />
		<bind param="nearClip" program="fp" />
		<bind param="farClip" program="fp" />
		<bind param="texDim" program="fp" />

		<bind param="lens_focal_distance" program="fp" />
		<bind param="lens_focal_length" program="fp" />
		<bind param="lens_fstop" program="fp" />
		<bind param="num_samples" program="fp" />
		<bind param="num_rings" program="fp" />
		<bind param="lens_coc" program="fp" />
		<bind param="lens_max_blur" program="fp" />

		<bind param="bokeh_threshold" program="fp" />
		<bind param="bokeh_gain" program="fp" />
		<bind param="bokeh_pentagon_enable" program="fp" />
		<bind param="bokeh_bias" program="fp" />
		<bind param="bokeh_fringe" program="fp" />
		<bind param="noise_amount" program="fp" />
		<bind param="bokeh_feather" program="fp" />

		<bind param="show_in_focus" program="fp" />
		<bind param="autofocus_enable" program="fp" />
		<bind param="autofocus_coordinates" program="fp" />


		<program name="vp" type="vertex"  >
<![CDATA[
#version 330 core

in vec3 position;
in vec2 texcoord;
out jit_PerVertex {
	smooth vec2 texcoord;
	smooth vec2 rectUV;
} jit_out;

uniform mat4 modelViewProjectionMatrix;
uniform mat4 textureMatrix0;

void main(void) {

	gl_Position = modelViewProjectionMatrix*vec4(position, 1.);
	jit_out.texcoord = texcoord;//vec2(texcoord.x, 1.0 - texcoord.y);
	jit_out.rectUV = vec2(textureMatrix0*vec4(texcoord, 0., 1.));
}
]]>
</program>

<program name="fp" type="fragment"  >
<![CDATA[
#version 330 core
#define TWOPI 6.28318530718

in jit_PerVertex {
	smooth vec2 texcoord;
	smooth vec2 rectUV;
} jit_in;

layout (location = 0) out vec4 outColor;

uniform sampler2DRect renderTex, depthTex, sampledDepthTex, blueNoiseTex;
uniform float nearClip, farClip, lens_focal_distance, lens_focal_length, lens_fstop;
uniform int num_samples, num_rings;
uniform float lens_coc, lens_max_blur, bokeh_threshold, bokeh_gain, bokeh_bias, bokeh_fringe, noise_amount, bokeh_feather;
uniform vec2 texDim, autofocus_coordinates;
uniform int bokeh_pentagon_enable, show_in_focus, autofocus_enable;

float penta(vec2 coords) //pentagonal shape
{
	float scale = float(num_rings) - 1.3;
	vec4  HS0 = vec4( 1.0,         0.0,         0.0,  1.0);
	vec4  HS1 = vec4( 0.309016994, 0.951056516, 0.0,  1.0);
	vec4  HS2 = vec4(-0.809016994, 0.587785252, 0.0,  1.0);
	vec4  HS3 = vec4(-0.809016994,-0.587785252, 0.0,  1.0);
	vec4  HS4 = vec4( 0.309016994,-0.951056516, 0.0,  1.0);
	vec4  HS5 = vec4( 0.0        ,0.0         , 1.0,  1.0);
	
	vec4  one = vec4( 1.0 );
	
	vec4 P = vec4((coords),vec2(scale, scale)); 
	
	vec4 dist = vec4(0.0);
	float inorout = -4.0;
	
	dist.x = dot( P, HS0 );
	dist.y = dot( P, HS1 );
	dist.z = dot( P, HS2 );
	dist.w = dot( P, HS3 );
	
	dist = smoothstep( -bokeh_feather, bokeh_feather, dist );
	
	inorout += dot( dist, one );
	
	dist.x = dot( P, HS4 );
	dist.y = HS5.w - abs( P.z );
	
	dist = smoothstep( -bokeh_feather, bokeh_feather, dist );
	inorout += dist.x;
	
	return clamp( inorout, 0.0, 1.0 );
}


vec2 rand() //generating noise/pattern texture for dithering
{
	vec2 blueNoise = texelFetch(blueNoiseTex, ivec2( mod( jit_in.rectUV, 64) ) ).xy*2 - 1;
	blueNoise.y *= texDim.x / texDim.y;
	return blueNoise;
	//float noiseX = clamp(fract(sin(dot(coord ,vec2(12.9898,78.233))) * 43758.5453),0.0,1.0)*2.0-1.0;
	//float noiseY = clamp(fract(sin(dot(coord ,vec2(12.9898,78.233)*2.0)) * 43758.5453),0.0,1.0)*2.0-1.0;
	//return vec2(noiseX,noiseY);
}

vec3 color(vec2 coords,float blur) //processing the sample
{
	vec2 amount = vec2(bokeh_fringe*blur) / texDim;
	vec3 col;
	col.r = texture(renderTex, texDim * (coords + vec2(0.0,1.0)*amount) 	).r;
	col.g = texture(renderTex, texDim * (coords + vec2(-0.866,-0.5)*amount) ).g;
	col.b = texture(renderTex, texDim * (coords + vec2(0.866,-0.5)*amount) 	).b;
	
	float lum = dot(col.rgb, vec3(0.299,0.587,0.114)); //luma
	float thresh = max((lum-bokeh_threshold)*bokeh_gain, 0.0);
	return col + mix(vec3(0.0),col,thresh*blur);
}

vec4 show_focus(float blur){

	float blur2 = blur*blur;
	float blur4 = blur2*blur2;
	float focal_plane = blur < 0.005 ? 1.0 : 0.0;

	float target = length( texDim * (jit_in.texcoord - autofocus_coordinates) ) < 10.0 ? 1.0 : 0.0;
	return vec4(	1 - blur + target,
					1 - blur2 - focal_plane - target,
					1 - blur4 - focal_plane,
					1);
}

void main(void) {
	
	float depth = texelFetch(depthTex, ivec2(jit_in.rectUV)).w;
	depth = depth == 0 ? 1.0 : depth;

	float fDepth = autofocus_enable == 0 ? lens_focal_distance / farClip : texelFetch(sampledDepthTex, ivec2(0.0)).r;

	float d = fDepth*1000.0; //focal plane in mm
	float o = depth*1000.0; //depth in mm
	
	float a = (o*lens_focal_length)/(o-lens_focal_length); 
	float b = (d*lens_focal_length)/(d-lens_focal_length); 
	float c = (d-lens_focal_length)/(d*lens_fstop*lens_coc); 
	
	float blur = clamp( abs(a-b)*c, 0.0, 1.0 );

	vec2 noise = rand()*noise_amount*blur;

	float max_blur = blur * lens_max_blur;
	float w = (1.0/texDim.x)*max_blur+noise.x;
	float h = (1.0/texDim.y)*max_blur+noise.y;
	
	vec3 col = texelFetch(renderTex, ivec2(jit_in.rectUV)).rgb;
	float s = 1.0;
	int ringsamples;
	
	for (int i = num_rings; i >= 1; i--)
	{   
		ringsamples = i * num_samples;
		float step = TWOPI / float(ringsamples);
		float k = mix(1.0, (float(i))/(float(num_rings)), bokeh_bias);
		
		for (int j = ringsamples-1; j >= 0; j--)   
		{
			float angle = float(j) * step;
			float pw = cos(angle) * float(i);
			float ph = sin(angle) * float(i);
			float p = bokeh_pentagon_enable == 1 ? penta(vec2(pw,ph)) * k : k;
			
			col += color(jit_in.texcoord + vec2(pw*w,ph*h), blur) * p;  
			s += p;   
		}
	}

	col /= s;
	
	outColor = show_in_focus == 0 ? vec4( col, fDepth ) : show_focus(blur);

}
]]>
</program>
</language>
</jittershader>
